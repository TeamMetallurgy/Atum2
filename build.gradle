buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven-publish'

repositories {
  maven {
    name 'JEI'
    url "http://dvs1.progwml6.com/files/maven"
  }
  maven {
    name 'Hwyla'
    url "http://tehnut.info/maven"
  }
  maven { 
    name 'CraftTweaker2'
    url "http://maven.blamejared.com" 
  }
  maven {
    name 'Baubles'
    url = "http://maven.thiakil.com"
  }
  maven {
    name 'TheOneProbe'
    url "https://maven.tterrag.com/"
  }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = "${minecraft_version}-${mod_version}"
group= "com.teammetallurgy.atum"
archivesBaseName = "Atum"

minecraft {
    version = "${minecraft_version}-${forge_version}"
    mappings = "${mappings_version}"
    runDir = "run"
	
    replace "@VERSION@", project.mod_version
    replaceIn "Constants.java"
	
    replace "@API_VERSION@", "${api_version}"
    replaceIn "package-info.java"
}

dependencies {
    deobfProvided "mezz.jei:jei_${minecraft_version}:${jei_version}:api"
    runtime "mezz.jei:jei_${minecraft_version}:${jei_version}"
    deobfProvided "mcp.mobius.waila:Hwyla:${hwyla_version}_${minecraft_version}"
    deobfProvided "CraftTweaker2:CraftTweaker2-MC1120-Main:${ct_version}"
	deobfProvided "com.azanor.baubles:Baubles:${baubles_version}:api"
	deobfProvided "mcjty.theoneprobe:TheOneProbe-1.12:${top_version}:api"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
	rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
	manifest {	
	    attributes 'FMLAT': 'atum_at.cfg'
	}
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allJava
	classifier = 'sources'
}

task apiJar(type: Jar) {
	from sourceSets.main.allSource
	from sourceSets.main.output
    include 'com/teammetallurgy/atum/api/**/*'
    classifier = 'api'
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifactId "atum2_${minecraft_version}"
            artifact jar
			artifact sourcesJar
			artifact apiJar
        }
    }
    repositories {
        if (project.hasProperty('maven_repo')) {
            maven { url maven_repo }
        } else {
            mavenLocal()
        }
    }
}

artifacts {
    archives sourcesJar
    archives apiJar
}