buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
  maven {
    url "http://dvs1.progwml6.com/files/maven"
  }
  maven {
    url "http://tehnut.info/maven"
  }
  maven { 
    url "http://maven.blamejared.com" 
  }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = "${minecraft_version}-${mod_version}" + ".alpha" + getBuildNumber()
group= "com.teammetallurgy.atum"
archivesBaseName = "Atum2"

minecraft {
    version = "${minecraft_version}-${forge_version}"
    mappings = "${mappings_version}"
    runDir = "run"
	
    replace "@VERSION@", project.mod_version
    replaceIn "Constants.java"
	
    replace "@API_VERSION@", "${api_version}"
    replaceIn "package-info.java"
}

dependencies {
    deobfProvided "mezz.jei:jei_${minecraft_version}:${jei_version}:api"
    runtime "mezz.jei:jei_${minecraft_version}:${jei_version}"
    deobfProvided "mcp.mobius.waila:Hwyla:${hwyla_version}_${minecraft_version}"
    deobfProvided "CraftTweaker2:CraftTweaker2-API:${ct_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
	rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
	manifest {	
	    attributes 'FMLAT': 'atum_at.cfg'
	}
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allJava
	classifier = 'sources'
}

task apiJar(type: Jar) {
	from sourceSets.main.allSource
	from sourceSets.main.output
    include 'com/teammetallurgy/atum/api/**/*'
    classifier = 'api'
}

artifacts {
    archives sourcesJar
    archives apiJar
}

String getBuildNumber() {
    return System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : System.getenv('TRAVIS_BUILD_NUMBER') ? System.getenv('TRAVIS_BUILD_NUMBER') : '0';
}